#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   tomchuk Ionescu <tomchuk.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nrGV6pg.png
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_tomchuk_async_callback {
  case $1 in
    prompt_tomchuk_async_git)
      # We can safely split on ':' because it isn't allowed in ref names.
      IFS=':' read _git_target _git_post_target <<<"$3"

      # The target actually contains 3 space separated possibilities, so we need to
      # make sure we grab the first one.
      _git_target=$(coalesce ${(@)${(z)_git_target}})

      if [[ -z "$_git_target" ]]; then
        # No git target detected, flush the git fragment and redisplay the prompt.
        if [[ -n "$_prompt_tomchuk_git" ]]; then
          _prompt_tomchuk_git=''
          zle && zle reset-prompt
        fi
      else
        # Git target detected, update the git fragment and redisplay the prompt.
        _prompt_tomchuk_git="${_git_target}${_git_post_target}"
        zle && zle reset-prompt
      fi
      ;;
    prompt_tomchuk_async_hg)
      # We can safely split on ':' because it isn't allowed in ref names.
      IFS=':' read _hg_target _hg_post_target <<<"$3"

      # The target actually contains 3 space separated possibilities, so we need to
      # make sure we grab the first one.
      _hg_target=$(coalesce ${(@)${(z)_hg_target}})

      if [[ -z "$_hg_target" ]]; then
        # No hg target detected, flush the git fragment and redisplay the prompt.
        if [[ -n "$_prompt_tomchuk_hg" ]]; then
          _prompt_tomchuk_hg=''
          zle && zle reset-prompt
        fi
      else
        # Hg target detected, update the git fragment and redisplay the prompt.
        _prompt_tomchuk_hg="${_hg_target}${_hg_post_target}"
        zle && zle reset-prompt
      fi
      ;;
  esac
}

function prompt_tomchuk_async_git {
  cd -q "$1"
  if (( $+functions[git-info] )); then
    git-info
    print ${git_info[status]}
  fi
}

function prompt_tomchuk_async_hg {
  cd -q "$1"
  if (( $+functions[hg-info] )); then
    hg-info
    print ${hg_info[status]}
  fi
}


function prompt_tomchuk_async_tasks {
  # Initialize async worker. This needs to be done here and not in
  # prompt_tomchuk_setup so the git formatting can be overridden by other prompts.
  if (( !${prompt_prezto_async_init:-0} )); then
    async_start_worker prompt_tomchuk -n
    async_register_callback prompt_tomchuk prompt_tomchuk_async_callback
    typeset -g prompt_prezto_async_init=1
  fi

  # Kill the old process of slow commands if it is still running.
  async_flush_jobs prompt_tomchuk

  # Compute slow commands in the background.
  async_job prompt_tomchuk prompt_tomchuk_async_git "$PWD"
  async_job prompt_tomchuk prompt_tomchuk_async_hg "$PWD"
}

function prompt_tomchuk_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_tomchuk_pwd=$(prompt-pwd)

  # Handle updating git data. We also clear the git prompt data if we're in a
  # different git root now.
  if (( $+functions[git-dir] )); then
    local new_git_root="$(git-dir 2> /dev/null)"
    if [[ $new_git_root != $_tomchuk_cur_git_root ]]; then
      _prompt_tomchuk_git=''
      _tomchuk_cur_git_root=$new_git_root
    fi
  fi

  # Handle updating hg data. We also clear the hg prompt data if we're in a
  # different hg root now.
  if (( $+functions[hg-dir] )); then
    local new_hg_root="$(hg-dir 2> /dev/null)"
    if [[ $new_hg_root != $_tomchuk_cur_hg_root ]]; then
      _prompt_tomchuk_hg=''
      _tomchuk_cur_hg_root=$new_hg_root
    fi
  fi

  # Run python info (this should be fast and not require any async)
  if (( $+functions[python-info] )); then
    python-info
  fi

  prompt_tomchuk_async_tasks
}

function prompt_tomchuk_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)
  _prompt_tomchuk_precmd_async_pid=0
  _prompt_tomchuk_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/tomchuk-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz async && async

  # Add hook for calling git-info & hg-info before each command.
  add-zsh-hook precmd prompt_tomchuk_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '%b %p %c:%s%A%B%S%a%d%m%r%U%u'

  # Set hg-info parameters.
  zstyle ':prezto:module:hg:info' verbose 'yes'
  zstyle ':prezto:module:hg:info:action' format ' %%B%F{5}%s%f%%b'
  zstyle ':prezto:module:hg:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:hg:info:branch' format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:hg:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:hg:info:deleted' format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:hg:info:modified' format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:hg:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:hg:info:shelved' format ' %%B%F{6}✭%f%%b'
  zstyle ':prezto:module:hg:info:unresolved' format ' %%B%F{9}✗%f%%b'
  zstyle ':prezto:module:hg:info:resolved' format ' %%B%F{10}✔%f%%b'
  zstyle ':prezto:module:hg:info:untracked' format ' %%B%F{7}◼%f%%b'
  zstyle ':prezto:module:hg:info:keys' format \
    'status' '%b %c:%s%S%a%d%m%R%U%u'

  # Set up non-zero return value display
  local show_return="✘ "
  # Default is to show the return value
  if zstyle -T ':prezto:module:prompt' show-return-val; then
    show_return+='%? '
  fi
  # Set python-info format
  zstyle ':prezto:module:python:info:virtualenv' format '%f%F{3}(%v)%F{7} '

  # Get the async worker set up
  _tomchuk_cur_git_root=''
  _tomchuk_cur_hg_root=''

  _prompt_tomchuk_git=''
  _promot_tomchuk_hg=''
  _prompt_tomchuk_pwd=''

  # Define prompts.
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  NL=$'\n'
  RPROMPT_PREFIX='%{'$'\e[1A''%}'
  RPROMPT_SUFFIX='%{'$'\e[1B''%}'
  PROMPT='${SSH_TTY:+"%F{9}%n%f%F{7}@%f%F{3}%m%f "}%F{4}${_prompt_tomchuk_pwd}%(!. %B%F{1}#%f%b.)${NL}${editor_info[keymap]} '
  RPROMPT='$RPROMPT_PREFIX$python_info[virtualenv]${editor_info[overwrite]}%(?:: %F{1}'
  RPROMPT+=${show_return}
  RPROMPT+='%f)${VIM:+" %B%F{6}V%f%b"}${_prompt_tomchuk_git}${_prompt_tomchuk_hg}$RPROMPT_SUFFIX'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_tomchuk_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'tomchuk'
}

prompt_tomchuk_setup "$@"
